plugins {
    id 'dev.architectury.loom' version '1.9.428' apply false
    id 'architectury-plugin' version '3.4-SNAPSHOT'
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    if (!it.name.startsWith('datapack')) {
        apply plugin: "dev.architectury.loom"

        loom {
            silentMojangMappingsLicense()
        }

        dependencies {
            minecraft "net.minecraft:minecraft:$rootProject.minecraft_version"
            mappings "net.fabricmc:yarn:$rootProject.yarn_mappings:v2"
        }
    }
}

allprojects {
    if (!it.name.startsWith('datapack')) {
        apply plugin: "java"
        apply plugin: "architectury-plugin"
        apply plugin: "maven-publish"

        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                name = 'TerraformersMC'
                url = 'https://maven.terraformersmc.com/'
            }
        }

        tasks.withType(JavaCompile) {
            options.encoding = "UTF-8"
            options.release = 17
        }

        java {
            withSourcesJar()
        }
    }

    base {
        archivesName = rootProject.archives_base_name
    }

    version = rootProject.mod_version
    group = rootProject.maven_group
}