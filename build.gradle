plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.7-SNAPSHOT" apply(false)
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    if (!it.name.startsWith('datapack')) {
        apply plugin: "dev.architectury.loom"

        loom {
            silentMojangMappingsLicense()
        }

        dependencies {
            minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
            mappings loom.layered {
                it.mappings("net.fabricmc:yarn:${rootProject.yarn_mappings}:v2")
                it.mappings("dev.architectury:yarn-mappings-patch-neoforge:1.21+build.4")
            }
        }
    }
}

allprojects {
    if (!it.name.startsWith('datapack')) {
        apply plugin: "java"
        apply plugin: "architectury-plugin"
        apply plugin: "maven-publish"

        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                name = 'TerraformersMC'
                url = 'https://maven.terraformersmc.com/'
            }
        }

        tasks.withType(JavaCompile) {
            options.encoding = "UTF-8"
            options.release = 17
        }

        java {
            withSourcesJar()
        }
    }

    base {
        archivesName = rootProject.archives_base_name
    }

    version = rootProject.mod_version
    group = rootProject.maven_group
}
